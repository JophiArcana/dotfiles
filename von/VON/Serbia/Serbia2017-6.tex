desc: Common tangent of circumcircle and excircle
source: Serbia 2017/6
tags: [2019-10, oly, tricky, geo, angle-chasing, projective, nice]

---

Let $ABC$ be a triangle and let the common external tangents to the circumcircle and the $A$-excircle intersect line $BC$ at $P$ and $Q$. Show that $\angle PAB=\angle CAQ$.

---

\paragraph{First solution, by elementary methods}     Let the tangents through $P$ and $Q$ touch $(ABC)$ at $P'$ and $Q'$ respectively, and let $L$ be the arc midpoint of $BC$ and $I_A$ the $A$-excenter.
\begin{center}
    \begin{asy}
        size(7cm); defaultpen(fontsize(10pt));
        pen pri=heavyblue;
        pen sec=lightblue;
        pen tri=deepcyan;
        pen qua=purple;
        pen qui=blue;
        pen fil=pri+opacity(0.05);
        pen sfil=sec+opacity(0.05);
        pen tfil=tri+opacity(0.05);

        pair O,A,B,C,L,I,IA,D,EE,F,T,Pp,Qp,Ppp,Qpp,P,Q;
        O=(0,0);
        A=dir(120);
        B=dir(210);
        C=dir(330);
        L=dir(270);
        I=incenter(A,B,C);
        IA=2L-I;
        D=foot(IA,B,C);
        EE=foot(IA,C,A);
        F=foot(IA,A,B);
        T=(abs(IA-D)*O-IA)/(abs(IA-D)-1);
        Pp=tangent(T,O,1,1);
        Qp=tangent(T,O,1,2);
        Ppp=tangent(T,IA,abs(IA-D),1);
        Qpp=tangent(T,IA,abs(IA-D),2);
        P=extension(B,C,T,Pp);
        Q=extension(B,C,T,Qp);

        draw(Pp--L--Qp,qua);
        draw(P--IA--Q,qua);
        filldraw(circumcircle(P,A,IA),tfil,tri);
        filldraw(circumcircle(Q,A,IA),tfil,tri);
        real t=1.3, s=2.2;
        draw(extension(T,Pp,(-1,t),(1,t))--extension(T,Pp,(-1,-s),(1,-s)),qui);
        draw(extension(T,Qp,(-1,t),(1,t))--extension(T,Qp,(-1,-s),(1,-s)),qui);
        filldraw(circle(IA,abs(IA-D)),sfil,sec);
        draw(A--IA,pri+Dotted);
        filldraw(circle(O,1),fil,pri);
        fill(A--B--C--cycle,fil);
        draw(P--Q,pri);
        draw(EE--A--F,pri);
        clip( (-100,t)--(100,t)--(100,-s)--(-100,-s)--cycle);

        dot("$A$",A,A);
        dot("$B$",B,NE);
        dot("$C$",C,dir(35));
        dot("$L$",L,SW);
        dot("$I_A$",IA,S);
        dot("$P'$",Pp,W);
        dot("$Q'$",Qp,dir(15));
        dot("$P$",P,W);
        dot("$Q$",Q,dir(10));
    \end{asy}
\end{center}
\setcounter{claim}0
\begin{claim}
    $\seg{P'L}\parallel\seg{PI_A}$ (and thus $\seg{Q'L}\parallel\seg{QI_A}$).
\end{claim}
\begin{proof}
    Note that $\seg{LL}\parallel\seg{BC}$, so $\seg{P'L}$ is parallel to the external angle bisector of $\angle BPP'$. Since the excircle is tangent to lines $BC$ and $PP'$, we know $\seg{PI_A}$ externally bisects $\angle BPP'$, so $\seg{P'L}\parallel\seg{PI_A}$, as desired.
\end{proof}
\begin{claim}
    $PP'AI_A$ is cyclic (thus so is $QQ'AI_A$).
\end{claim}
\begin{proof}
    This is obvious from the above claim: $\da PP'A=\da P'LA=\da PI_AA$.
\end{proof}

Finally, $\measuredangle PAI_A=\measuredangle PP'I_A=\measuredangle I_AQ'Q=\measuredangle I_AAQ$, so $\seg{AP}$ and $\seg{AQ}$ are isogonal with respect to $\angle BAC$, as desired.

\paragraph{Second solution, by Desargue involution}     Let $S$ be the exsimilicenter and $T$ the tangency point between $(ABC)$ and the $A$-mixtillinear incircle. By Monge's theorem, $A$, $T$, $S$ are collinear.

Let the excircle touch $\seg{BC}$ at $D$, and let $X=\seg{AST}\cap\seg{BC}$. By the dual of Desargue's involution theorem from $S$ to $ABDC$ (with the excircle as the inconic), we have the involutive pairing $S(AD;BC;PQ)$. Projecting onto $\seg{BC}$ gives $(XD;BC;PQ)$, and perspectivity through $A$ gives $A(TD;BC;PQ)$.

But $\seg{AT}$ and $\seg{AD}$ are isogonal in $\angle A$, so $\seg{AP}$ and $\seg{AQ}$ are also isogonal, and we are done.


